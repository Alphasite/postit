/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.3/userguide/tutorial_java_projects.html
 */

buildscript {
    repositories {
        mavenCentral()
        //Needed only for SNAPSHOT versions
        //maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.11'
    }
}

// Apply the java plugin to add support for Java
apply plugin: 'java'

// Apply the application plugin to add support for building an application
apply plugin: 'application'

apply plugin: "info.solidsoft.pitest"

apply plugin: "jacoco"

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    mavenCentral()
}

dependencies {
    // The production code uses Guava
    compile group: 'javax.json', name: 'javax.json-api', version: '1.0'
    compile group: 'org.glassfish', name: 'javax.json', version: '1.0.4'
    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.56'
    compile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.56'

    // Use JUnit test framework
    testCompile 'junit:junit:4.12'
    testCompile 'commons-io:commons-io:2.5'
    testCompile 'com.h2database:h2:1.4.194'
    testCompile 'ch.vorburger.mariaDB4j:mariaDB4j:2.2.2'

    compile 'mysql:mysql-connector-java:6.0.+'
    compile 'org.json:json:20160212'
}

// Define the main class for the application
mainClassName = 'App'
sourceCompatibility = 1.8

pitest {
    targetClasses = ['postit.*']  //by default "${project.group}.*"
    pitestVersion = "1.1.0" //not needed when a default PIT version should be used
    threads = 4
    outputFormats = ['XML', 'HTML']
    reportDir = "build/pit-reports/"
    excludedClasses = ['postit.server.*']
//    avoidCallsTo = ['postit.server.*']
}

tasks.withType(Test) {
    testLogging {
        // set options for log level LIFECYCLE
        events "passed", "skipped", "failed", "standardOut"
        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true
        showStandardStreams true

        // set options for log level DEBUG and INFO
        debug {
            events "started", "passed", "skipped", "failed", "standardOut", "standardError"
            exceptionFormat "full"
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

//test {
//    testLogging.showStandardStreams = true
//}